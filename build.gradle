plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.3'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.bank'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	agent
}

repositories {
	mavenCentral()
}

dependencies {
	agent("io.opentelemetry.javaagent:opentelemetry-javaagent:1.32.0")

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.h2database:h2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('copyAgent', Copy) {
	from configurations.agent {
		rename "opentelemetry-javaagent-.*\\.jar", "opentelemetry-javaagent.jar"
	}
	into layout.buildDirectory.dir("agent")
}

bootJar {
	dependsOn copyAgent

	archiveFileName = "app.jar"
}
